/*
 * Copyright (C) 2014 Vandal LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.localRepo = file("$buildDir/repo")

    repositories {
        maven { url(localRepo) }
        maven { url("https://oss.sonatype.org/content/repositories/snapshots/") }
        mavenCentral()
    }
    dependencies {
        classpath "com.vandalsoftware.tools.gradle:stalker:+"
    }
}

apply plugin: "groovy"
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'stalker'

sourceCompatibility = JavaVersion.VERSION_1_7

group = "com.vandalsoftware.tools.gradle"

ext.release = has("release")
ext.sonatypeRepo = has("sonatypeRepo") ? sonatypeRepo : null
ext.sonatypeSnapshotRepo = has("sonatypeSnapshotRepo") ? sonatypeSnapshotRepo : null
ext.sonatypeUsername = has("sonatypeUsername") ? sonatypeUsername : null
ext.sonatypePassword = has("sonatypePassword") ? sonatypePassword : null

version = getVersion("0.4.1")

jar {
    baseName = "stalker"
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

task publishLocal(type: Upload) {
    configuration = configurations.default
    repositories {
        mavenDeployer {
            repository(url: localRepo.toURI())
        }
    }
}

signing {
    if (project.ext.release) {
        sign configurations.archives
    }
}

uploadArchives {
    onlyIf {
        (project.ext.sonatypeRepo || project.ext.sonatypeSnapshotRepo) &&
                project.ext.sonatypePassword && project.ext.sonatypeUsername
    }
    repositories {
        mavenDeployer {
            configuration = configurations.archives
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            def repo
            if (project.ext.release) {
                repo = project.ext.sonatypeRepo
            } else {
                repo = project.ext.sonatypeSnapshotRepo
            }
            repository(url: "$repo") {
                authentication(userName: project.ext.sonatypeUsername,
                        password: project.ext.sonatypePassword)
            }
            pom.artifactId = "stalker"
            pom.project {
                name 'Stalker'
                packaging 'jar'
                description 'Class analyzing Gradle plugin'
                url 'https://github.com/VandalSoftware/stalker'

                scm {
                    url 'scm:git@github.com:VandalSoftware/stalker.git'
                    connection 'scm:git@github.com:VandalSoftware/stalker.git'
                    developerConnection 'scm:git@github.com:VandalSoftware/stalker.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jle'
                        name 'Jonathan Le'
                        email 'jle@vandal.io'
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.eclipse.jgit:org.eclipse.jgit:3.3.0.201403021825-r'
    testCompile 'com.android.tools.build:gradle:0.9.+'
}

def getVersion(String baseVersion) {
    if (project.ext.release) {
        return baseVersion
    }
    return baseVersion + '-SNAPSHOT'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

stalk.dependsOn testClasses
